#version 330 core

// Atributos de fragmentos recebidos como entrada ("in") pelo Fragment Shader.
// Neste exemplo, este atributo foi gerado pelo rasterizador como a
// interpola√ß√£o da posi√ß√£o global e a normal de cada v√©rtice, definidas em
// "shader_vertex.glsl" e "main.cpp".
in vec4 position_world;
in vec4 normal;

// Posi√ß√£o do v√©rtice atual no sistema de coordenadas local do modelo.
in vec4 position_model;

// Coordenadas de textura obtidas do arquivo OBJ (se existirem!)
in vec2 texcoords;

// Matrizes computadas no c√≥digo C++ e enviadas para a GPU
uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

// Identificador que define qual objeto est√° sendo desenhado no momento
#define SPHERE 0
#define BUNNY  1
#define PLANE  2
uniform int object_id;

// Par√¢metros da axis-aligned bounding box (AABB) do modelo
uniform vec4 bbox_min;
uniform vec4 bbox_max;

// Vari√°veis para acesso das imagens de textura
uniform sampler2D TextureImage0;
uniform sampler2D TextureImage1;
uniform sampler2D TextureImage2;

// O valor de sa√≠da ("out") de um Fragment Shader √© a cor final do fragmento.
out vec4 color;

// Constantes
#define M_PI   3.14159265358979323846
#define M_PI_2 1.57079632679489661923

void main()
{
    // Obtemos a posi√ß√£o da c√¢mera utilizando a inversa da matriz que define o
    // sistema de coordenadas da c√¢mera.
    vec4 origin = vec4(0.0, 0.0, 0.0, 1.0);
    vec4 camera_position = inverse(view) * origin;

    // O fragmento atual √© coberto por um ponto que percente √† superf√≠cie de um
    // dos objetos virtuais da cena. Este ponto, p, possui uma posi√ß√£o no
    // sistema de coordenadas global (World coordinates). Esta posi√ß√£o √© obtida
    // atrav√©s da interpola√ß√£o, feita pelo rasterizador, da posi√ß√£o de cada
    // v√©rtice.
    vec4 p = position_world;

    // Normal do fragmento atual, interpolada pelo rasterizador a partir das
    // normais de cada v√©rtice.
    vec4 n = normalize(normal);

    // Vetor que define o sentido da fonte de luz em rela√ß√£o ao ponto atual.
    vec4 l = normalize(vec4(1.0,1.0,0.0,0.0));

    // Vetor que define o sentido da c√¢mera em rela√ß√£o ao ponto atual.
    vec4 v = normalize(camera_position - p);

    // Coordenadas de textura U e V
    float U = 0.0;
    float V = 0.0;

    if ( object_id == SPHERE )
    {
        // PREENCHA AQUI as coordenadas de textura da esfera, computadas com
        // proje√ß√£o esf√©rica EM COORDENADAS DO MODELO. Utilize como refer√™ncia
        // o slides 134-150 do documento Aula_20_Mapeamento_de_Texturas.pdf.
        // A esfera que define a proje√ß√£o deve estar centrada na posi√ß√£o
        // "bbox_center" definida abaixo.

        // Voc√™ deve utilizar:
        //   fun√ß√£o 'length( )' : comprimento Euclidiano de um vetor
        //   fun√ß√£o 'atan( , )' : arcotangente. Veja https://en.wikipedia.org/wiki/Atan2.
        //   fun√ß√£o 'asin( )'   : seno inverso.
        //   constante M_PI
        //   vari√°vel position_model

        //vec4 bbox_center = (bbox_min + bbox_max) / 2.0;
        
        // Usando vec 3 pra nao dar problema com o length
        vec3 bbox_center = ((bbox_min + bbox_max) * 0.5).xyz;
        vec3 p = position_model.xyz;

        vec3 p_rel = p - bbox_center;

        // p_rep eh o ponto relativo ao centro da esfera
        // e rho eh o raio da esfera
        float rho = length(p_rel);

        // usamos o ponto relativo e o raio para determinar os angulos phi e theta
        // fazendo a inversa da f√≥rmula aprendida em aula:
        // ùê©ùë• = ùúå cosùúë sin ùúÉ
        // ùê©ùë¶ = ùúå sin ùúë
        // ùê©ùëß = ùúå cosùúë cos ùúÉ

        // ùê©ùë¶ = ùúå sin ùúë -> ùúë = asin( ùê©ùë¶ / ùúå )
        float phi = asin(p_rel.y / rho);

        // c√°lculo de Œ∏ usando a fun√ß√£o atan2 para obter o √¢ngulo correto no plano xz
        float theta = atan(p_rel.x, p_rel.z);

        U = 0.5 + theta / (2.0 * M_PI);
        V = 0.5 + phi / M_PI;
    


    }
    else if ( object_id == BUNNY )
    {
        // PREENCHA AQUI as coordenadas de textura do coelho, computadas com
        // proje√ß√£o planar XY em COORDENADAS DO MODELO. Utilize como refer√™ncia
        // o slides 99-104 do documento Aula_20_Mapeamento_de_Texturas.pdf,
        // e tamb√©m use as vari√°veis min*/max* definidas abaixo para normalizar
        // as coordenadas de textura U e V dentro do intervalo [0,1]. Para
        // tanto, veja por exemplo o mapeamento da vari√°vel 'p_v' utilizando
        // 'h' no slides 158-160 do documento Aula_20_Mapeamento_de_Texturas.pdf.
        // Veja tamb√©m a Quest√£o 4 do Question√°rio 4 no Moodle.

        float minx = bbox_min.x;
        float maxx = bbox_max.x;

        float miny = bbox_min.y;
        float maxy = bbox_max.y;

        float minz = bbox_min.z;
        float maxz = bbox_max.z;

        float x = position_model.x;
        float y = position_model.y;

        U = (x - minx) / (maxx - minx);
        V = (y - miny) / (maxy - miny);
        
    }
    else if ( object_id == PLANE )
    {
        // Coordenadas de textura do plano, obtidas do arquivo OBJ.
        U = texcoords.x;
        V = texcoords.y;
    }

    // Obtemos a reflet√¢ncia difusa a partir da leitura da imagem TextureImage0
    vec3 Kd0 = texture(TextureImage0, vec2(U,V)).rgb;

    // Equa√ß√£o de Ilumina√ß√£o
    float lambert = max(0,dot(n,l));

    color.rgb = Kd0 * (lambert + 0.01);

    // NOTE: Se voc√™ quiser fazer o rendering de objetos transparentes, √©
    // necess√°rio:
    // 1) Habilitar a opera√ß√£o de "blending" de OpenGL logo antes de realizar o
    //    desenho dos objetos transparentes, com os comandos abaixo no c√≥digo C++:
    //      glEnable(GL_BLEND);
    //      glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA);
    // 2) Realizar o desenho de todos objetos transparentes *ap√≥s* ter desenhado
    //    todos os objetos opacos; e
    // 3) Realizar o desenho de objetos transparentes ordenados de acordo com
    //    suas dist√¢ncias para a c√¢mera (desenhando primeiro objetos
    //    transparentes que est√£o mais longe da c√¢mera).
    // Alpha default = 1 = 100% opaco = 0% transparente
    color.a = 1;

    // Cor final com corre√ß√£o gamma, considerando monitor sRGB.
    // Veja https://en.wikipedia.org/w/index.php?title=Gamma_correction&oldid=751281772#Windows.2C_Mac.2C_sRGB_and_TV.2Fvideo_standard_gammas
    color.rgb = pow(color.rgb, vec3(1.0,1.0,1.0)/2.2);
} 

